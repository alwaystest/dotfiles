" Specify a directory for plugins
" - For Neovim: ~/.local/share/nvim/plugged
" - Avoid using standard Vim directory names like 'plugin'
call plug#begin('~/.vim/plugged')

" Make sure you use single quotes

" Plugin outside ~/.vim/plugged with post-update hook
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
Plug 'embear/vim-foldsearch'
Plug 'scrooloose/vim-slumlord'
Plug 'aklt/plantuml-syntax'
Plug 'flazz/vim-colorschemes'
Plug 'tpope/vim-fugitive'
Plug 'airblade/vim-gitgutter'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'Yggdroot/indentLine'
Plug 'luochen1990/rainbow'
Plug 'sheerun/vim-polyglot'
Plug 'tpope/vim-surround'
Plug 'scrooloose/nerdcommenter'
Plug 'vim-syntastic/syntastic'
Plug 'jiangmiao/auto-pairs'
Plug 'sbdchd/neoformat'
Plug 'easymotion/vim-easymotion'
Plug 'vim-test/vim-test'
Plug 'jebaum/vim-tmuxify'
Plug 'github/copilot.vim'

" Defx
if has('nvim')
  Plug 'Shougo/defx.nvim', { 'do': ':UpdateRemotePlugins' }
else
  Plug 'Shougo/defx.nvim'
  Plug 'roxma/nvim-yarp'
  Plug 'roxma/vim-hug-neovim-rpc'
  Plug 'kristijanhusak/defx-icons'
endif

Plug 'honza/vim-snippets'
Plug 'neoclide/coc.nvim', {'branch': 'release'}
Plug 'masukomi/vim-markdown-folding'

" Initialize plugin system
call plug#end()

" Similarly, we can apply it to fzf#vim#grep. To use ripgrep instead of ag:
command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   'rg --column --line-number --no-heading --color=always '.shellescape(<q-args>), 1,
  \   <bang>0 ? fzf#vim#with_preview('up:60%')
  \           : fzf#vim#with_preview('right:50%:hidden', '?'),
  \   <bang>0)

function! s:find_git_root()
  return system('git rev-parse --show-toplevel 2> /dev/null')[:-2]
endfunction
command! ProjectFiles execute 'Files' s:find_git_root()

if OSX()
    " fzf
    let g:ctrlp_map = ''
    nnoremap <C-p> :Files<CR>
    nnoremap <leader>b :Buffers<CR>
    nnoremap <leader>m :Marks<CR>
    nnoremap <leader>r :Rg<CR>
endif
color gruvbox
:map <F1> <nop>

" Plugins {

    " defx {

        if isdirectory(expand("~/.vim/plugged/defx.nvim"))

            call defx#custom#option('_', {
                  \ 'columns': 'mark:indent:icons:filename:type',
                  \ 'winwidth': 30,
                  \ 'split': 'vertical',
                  \ 'direction': 'topleft',
                  \ 'show_ignored_files': 0,
                  \ 'ignored_files': '*.pyc,*.pyd,*~,*.swo,*.swp,.git,.hg,.svn,.bzr,.DS_Store',
                  \ })

            call defx#custom#column('mark', {
                  \ 'readonly_icon': '',
                  \ 'selected_icon': '✓',
                  \ })

            call defx#custom#column('filename', {
                  \ 'min_width': 5,
                  \ 'max_width': 25,
                  \ })

            call defx#custom#column('time', {
                  \ 'format': '%Y %b %e %H:%M:%S',
                  \ })

            " let g:vimfiler_as_default_explorer = 1

            function! s:defx_toggle_tree_or_open_file() abort
              if defx#is_directory()
                return defx#do_action('open_or_close_tree')
              else
                return defx#do_action('drop')
              endif
            endfunction

            function! s:defx_cd_or_open_file() abort
              if defx#is_directory()
                return defx#do_action('open_directory')
              else
                return defx#do_action('drop')
              endif
            endfunction

            function! s:defx_keymaps() abort
              " double click/Enter/l to open file
              nnoremap <silent><buffer><expr> <2-LeftMouse> <sid>defx_toggle_tree_or_open_file()
              nnoremap <silent><buffer><expr> <CR> <sid>defx_toggle_tree_or_open_file()
              nnoremap <silent><buffer><expr> l    <sid>defx_cd_or_open_file()

              nnoremap <silent><buffer><expr> q     defx#do_action('quit')
              nnoremap <silent><buffer><expr> .     defx#do_action('toggle_ignored_files')
              nnoremap <silent><buffer><expr> yy    defx#do_action('yank_path')
              nnoremap <silent><buffer><expr> ~     defx#do_action('cd')
              nnoremap <silent><buffer><expr><nowait> \  defx#do_action('cd', getcwd())
              nnoremap <silent><buffer><expr> h     defx#do_action('cd', ['..'])

              nnoremap <silent><buffer><expr><nowait> <Space> defx#do_action('toggle_select') . 'j'
              nnoremap <silent><buffer><expr> *      defx#do_action('toggle_select_all')
              nnoremap <silent><buffer><expr> <C-c>  defx#do_action('clear_select_all')
              nnoremap <silent><buffer><expr> <C-r>  defx#do_action('redraw')
              nnoremap <silent><buffer><expr> <C-g>  defx#do_action('print')

              nnoremap <silent><buffer><expr> K     defx#do_action('new_directory')
              nnoremap <silent><buffer><expr> N     defx#do_action('new_multiple_files')
              nnoremap <silent><buffer><expr> dd    defx#do_action('remove')
              nnoremap <silent><buffer><expr> r     defx#do_action('rename')
              nnoremap <silent><buffer><expr> !     defx#do_action('execute_command')
              nnoremap <silent><buffer><expr><nowait> c  defx#do_action('copy')
              nnoremap <silent><buffer><expr><nowait> m  defx#do_action('move')
              nnoremap <silent><buffer><expr><nowait> p  defx#do_action('paste')

              nnoremap <silent><buffer><expr> S  defx#do_action('toggle_sort', 'time')
            endfunction

            " Ref:
            " https://github.com/Shougo/vimfiler.vim/blob/edbb2f2e6baa66c51f73a82afa2bb740415a64ea/plugin/vimfiler.vim#L72
            " https://github.com/Shougo/defx.nvim/issues/121
            function! s:browse() abort
              let l:path = expand('<amatch>')
              if l:path ==# '' || bufnr('%') != expand('<abuf>')
                return
              endif

              if &filetype ==# 'defx' && line('$') != 1
                return
              endif

              if !isdirectory(l:path)
                return
              endif

              bd
              exe ':Defx -split=no -columns=mark:indent:icons:filename:type:size:time ' . l:path
            endfunction

            " disable netrw.
            augroup FileExplorer
              autocmd!
            augroup END

            augroup defx_group
              autocmd!
              " auto close if it is the last
              autocmd BufEnter * if (&buftype ==# 'defx' || &buftype ==# 'nofile')
                    \ && (!has('vim_starting'))
                    \ && winbufnr(2) == -1 | quit! | endif
              " Move focus to the next window if current buffer is defx
              autocmd TabLeave * if &ft ==# 'defx' | wincmd w | endif
              " keymap
              autocmd FileType defx do WinEnter | call s:defx_keymaps()
              " replace netrw to defx
              autocmd BufEnter * call s:browse()
            augroup END

            map <C-e> :Defx -toggle<CR>
        endif
    " }

    " test {
      " run test cases in current file
      nmap <silent> t<C-f> :TestFile<CR>

      " make test commands execute using dispatch.vim
      let test#strategy = "tmuxify"
    " }
" }

